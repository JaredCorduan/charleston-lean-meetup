/-

== Inductive Types ==

inductive Foo where
  | constructor‚ÇÅ : ... ‚Üí Foo
  | constructor‚ÇÇ : ... ‚Üí Foo
  ...
  | constructor‚Çô : ... ‚Üí Foo

-/


-- Example: Lists

inductive L (Œ± : Sort u) where
  | nil : L Œ±
  | cons : Œ± ‚Üí L Œ± ‚Üí L Œ±

example : L Nat := L.cons 17 (L.cons 42 L.nil)


def length (xs : L Œ±) : Nat :=
  match xs with
  | L.nil => 0
  | L.cons _ xs => 1 + length xs


-- Example: Optional / Maybe

inductive Opt Œ± where
  | nothing : Opt Œ±
  | just : Œ± ‚Üí Opt Œ±


def head : L Œ± ‚Üí Opt Œ±
  | L.nil => Opt.nothing
  | L.cons x _ => Opt.just x



-- Example: Natural Numbers 0, 1, 2, ‚Ä¶

inductive N where
  | zero : N
  | succ : N ‚Üí N
  deriving Repr -- for #eval


def one := N.succ N.zero
def two := N.succ one
def three := N.succ two



def addN (m n : N) : N :=
  match n with
  | N.zero   => m
  | N.succ n => N.succ (addN m n)


#eval addN one two
example : addN one two = three := rfl


-- The Add typeclass lets us use the plus symbol
instance : Add N where
  add := addN


/-

GOALS for N:

1) (x + y) + z = x + (y + z)

2) x + y = y + x

3) m + n = p + n ‚Üí m = p

Tools:

1) rfl
2) structural recursion
3) rewriting (rw tactic)
4) congrArg: IF a = b THEN f(a) = f(b)

-/


-- Two easy theorems:

theorem add_zero (m : N) : m + N.zero = m := rfl

-- Notice this fails to work:
--theorem zero_add (m : N) : N.zero + m = m := rfl

#print add_zero


theorem add_succ (m n : N) : m + (N.succ n) = N.succ (m + n) := rfl



-- DETOUR: the magic of RFL
-- Note we haven't seen Indexed Families yet

inductive IsTheSameAs {Œ± : Sort u} (a : Œ±) : Œ± ‚Üí Prop where
  | funnel_of_truth : IsTheSameAs a a

theorem one_and_one_is_two : IsTheSameAs (one + one) two := IsTheSameAs.funnel_of_truth

infix:50 " üçï " => IsTheSameAs
theorem one_and_one_is_still_two : one + one üçï two := IsTheSameAs.funnel_of_truth



-- Back to arithmetic!


theorem succ_add (m n : N) : N.succ m + n = N.succ (m + n) :=
  match n with
  | N.zero => rfl
  | N.succ x => by rw [add_succ, add_succ, succ_add]
  --| N.succ x => congrArg N.succ (succ_add m x)

#print congrArg



theorem zero_add (n : N) : N.zero + n = n :=
  match n with
  | N.zero => rfl
  | N.succ x => congrArg N.succ (zero_add x)

#print zero_add



theorem add_comm (m n : N) : m + n = n + m :=
  match n with
  | N.zero => by rw [zero_add, add_zero]
  | N.succ x => by rw [succ_add, add_succ, add_comm]

  --| N.succ x =>
  --  have h1 : N.succ (m + x) = N.succ (x + m) := congrArg N.succ (add_comm m x)
  --  have h2 : N.succ x + m = N.succ (x + m) := succ_add x m
  --  have h3 : N.succ (x + m) = N.succ x + m := Eq.symm h2
  --  have h4 : m + N.succ x = N.succ x + m := Eq.trans h1 h3
  --  h4

  --| N.succ x => Eq.trans (congrArg N.succ (add_comm m x)) (Eq.symm (succ_add x m))


theorem add_assoc (m n p : N) : m + (n + p) = (m + n) + p :=
  match p with
  | N.zero => rfl
  | N.succ q => -- show  m + (n + (q+1)) = (m + n) + (q + 1)
      have h1 : m + (n + q) = m + n + q := add_assoc m n q
      have h2 : N.succ (m + (n + q)) = N.succ (m + n + q) := congrArg N.succ h1
      have h3 : m + N.succ (n + q) = m + n + N.succ q := h2
      have h4 : m + (n + N.succ q) = m + n + N.succ q := h3
      h4
  --| N.succ q => congrArg N.succ (add_assoc m n q)


def pred (m : N) : N :=
  match m with
  | N.zero => N.zero
  | N.succ n => n

theorem succ_inj {m n : N} (h : N.succ m = N.succ n) : m = n := congrArg pred h

theorem add_cancel_right {m n p : N} (h : m + n = p + n) : m = p :=
  match n with
  | N.zero => h
  | N.succ _ => add_cancel_right (succ_inj h)

theorem add_cancel_left {m n p : N} (h : m + n = m + p) : n = p :=
  have h1 : n + m = p + m := by rw [add_comm, add_comm m p] at h; exact h
  add_cancel_right h1




/-

üéâ GOALS COMPLETE üéâ

What could we do next?

* Multiplication
* Arithmetic facts
* Binary notation
* Integers

-/


-- Multiplication

def mult (m n : N) : N :=
  match n with
  | N.zero   => N.zero
  | N.succ n => (mult m n) + m

instance : Mul N where
  mul := mult

theorem mult_assoc (m n p : N) : (m * n) * p = m * (n * p) := sorry
theorem mult_comm (m n : N) : m * n = n * m := sorry
theorem mult_distr_add (m n p: N) : m * (n + p) = (m * n) + (m * p) := sorry




-- Binary representation of numbers

inductive Bin where
  | Œµ : Bin
  | O : Bin ‚Üí Bin
  | I : Bin ‚Üí Bin


open Bin

notation:50 x "‚óØ" => Bin.O x
notation:50 x "‚Ö†" => Bin.I x


def eight := Œµ ‚Ö† ‚óØ ‚óØ ‚óØ



def incr (b : Bin) : Bin :=
  match b with
  | Œµ   => Œµ ‚Ö†
  | Œµ ‚Ö† => Œµ ‚Ö† ‚óØ
  | b ‚óØ => b ‚Ö†
  | b ‚Ö† => (incr b) ‚óØ

def toN (b : Bin) : N :=
  match b with
  | Œµ     => N.zero
  | b' ‚óØ => toN b' + toN b'
  | b' ‚Ö†  => toN b' + toN b' + N.succ N.zero

def toB (n : N) : Bin :=
  match n with
  | N.zero => Œµ ‚óØ
  | N.succ n => incr (toB n)

-- Prove if true, find counterexample otherwise

theorem incr_succ (b : Bin) : toN (incr b) = N.succ (toN b) := sorry

theorem to_from (b : Bin) : toB (toN b) = b := sorry

theorem from_to (n : N) : toN (toB n) = n := sorry




-- Integers
--
-- we represent x ‚àà ‚Ñ§ by (x, y)
-- where (x, y) is thought of as x - y

inductive Z where
  | mk : N ‚Üí N ‚Üí Z

-- (a - b) = (c - d) IFF a + d = c + b
def Zequiv (x y : Z) : Prop :=
  match (x, y) with
  | (Z.mk a b, Z.mk c d) => a + d = c + b

infix:50 " ~ " => Zequiv

-- Zequiv is an Equivalenc Relation
--
-- Reflexive:  x ~ x
-- Symetric:   IF x ~ y THEN y ~ x
-- Transitive: IF x ~ y AND y ~ z THEN x ~ z

theorem ze_refl (x : Z) : x ~ x := rfl

theorem ze_symm : ‚àÄ {x y : Z}, x ~ y ‚Üí y ~ x
  | Z.mk a b, Z.mk c d => Œª h =>
    have h': c + b = a + d := by rw [h]
    h'

theorem add_eqns : ‚àÄ {a b c d : N}, a = b ‚Üí c = d ‚Üí a + c = b + d :=
  Œª h1 => Œª h2 => by rw [h1, h2]

theorem ze_trans : ‚àÄ {x y z : Z}, x ~ y ‚Üí y ~ z ‚Üí x ~ z
  | Z.mk a b, Z.mk c d, Z.mk e f =>
    Œª hxy => Œª hyz =>
      have h: (a + d) + (c + f) = (c + b) + (e + d) := add_eqns hxy hyz

      -- Plan:
      --   use commutivity and associativity to move c and d so
      --   where they can be canceled

      -- Move d + c to the far right on the RHS
      have hr: (a + f) + (d + c) = (c + b) + (e + d) := by
        rw [ add_comm c f
           , ‚Üê add_assoc
           , add_assoc d f c
           , add_comm d f
           , ‚Üê add_assoc f d c
           , add_assoc
           ] at h;
        exact h

      -- Move d + c to the far right on the LHS
      have hl: (a + f) + (d + c) = (e + b) + (d + c) := by
        rw [ add_comm c b
           , ‚Üê add_assoc b c
           , add_assoc c e d
           , add_comm c e
           , ‚Üê add_assoc e c d
           , add_assoc b e
           , add_comm c d
           , add_comm b e
           ] at hr; exact hr

      add_cancel_right hl


theorem ze_equivalence : Equivalence Zequiv :=
  { refl := ze_refl, symm := ze_symm, trans := ze_trans }

instance : Setoid Z where
  r     := Zequiv
  iseqv := ze_equivalence

def neg_two := Quotient.mk' (Z.mk N.zero two)
#check neg_two
